dnl -*- mode: m4; -*-
dnl Test suite for rifiuti2

AT_SETUP([Err on non-existent input])
AT_KEYWORDS([file])
_r2_check_ignore_err([foobar], [2])
AT_CLEANUP

AT_SETUP([Err on non-regular file (Unix)])
AT_KEYWORDS([file])
AT_CHECK([echo "$host_os" | grep -i -q 'MINGW\|MSYS' && exit 77 || exit 0])
_r2_check_ignore_err([/dev/zero], [2])
AT_CLEANUP

dnl g_file_test() was not so usable on Windows: NUL, CON etc are considered as regular files.
dnl Therefore file can't be determined early as unusable, they have to be actually parsed.
AT_SETUP([Err on non-regular file (Windows)])
AT_KEYWORDS([file])
AT_CHECK([echo "$host_os" | grep -i -q 'MINGW\|MSYS' || exit 77])
_r2_check_ignore_err([nul], [3])
AT_CLEANUP

AT_SETUP([UTF-8 console/file output are equal])
AT_KEYWORDS([file])
AT_CHECK([
	$progf -8 -o expout $sample/INFO2-sample1
	$progf -8           $sample/INFO2-sample1
],, [expout], [])
AT_CHECK([
	$progd -8 -o expout $sample/dir-sample1
	$progd -8           $sample/dir-sample1
],, [expout], [])
AT_CLEANUP

AT_SETUP([Read non-ASCII path])
AT_KEYWORDS([file encoding])
AT_CHECK([$progf $sample/ごみ箱/INFO2-empty],, [ignore], [ignore])
AT_CHECK([$progd $sample/ごみ箱/dir-empty],, [ignore], [ignore])
AT_CLEANUP

dnl vim: set ts=4 sw=4 noexpandtab :
